plugins {
    id 'idea'
    id 'java'
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.20.0"
    id "com.google.protobuf" version "0.8.17"
}

group 'boozill.houston'
version '1.0.2'

repositories {
    mavenCentral()
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.18.1'
    }

    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.44.0'
        }
    }

    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    implementation 'io.grpc:grpc-all:1.44.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    compileOnly 'org.projectlombok:lombok:1.18.22'
    testCompileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'
}

test {
    useJUnitPlatform()
}

gradlePlugin {
    plugins {
        houston {
            id = 'io.github.Boozilla.houston'
            implementationClass = 'boozilla.houston.gradle.HoustonPlugin'
        }
    }
}

pluginBundle {
    mavenCoordinates {
        groupId = "io.github.Boozilla.houston"
        artifactId = "houston-gradle-plugin"
        version = project.version
    }

    website = 'https://github.com/Boozilla/houston-gradle'
    vcsUrl = 'https://github.com/Boozilla/houston-gradle'

    description = 'It is a protobuf structure synchronization helper plugin that works with the Houston asset server'

    plugins {
        houston {
            displayName = 'Houston asset structure sync plugin'
            tags = ['houston', 'asset', 'sync']
        }
    }
}