plugins {
    id 'java-library'
    alias libs.plugins.protobuf
    alias libs.plugins.publish
}

group = rootProject.group
version = rootProject.version

sourceSets {
    main {
        proto {
            srcDir rootProject.sourceSets.main.proto.srcDirs
        }
    }
}

protobuf {
    def protobufVersion = libs.versions.protobuf.asProvider().get()
    def grpcVersion = libs.versions.grpc.get()
    def reactorGrpcVersion = libs.versions.reactor.grpc.get()

    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
        reactor {
            artifact = "com.salesforce.servicelibs:reactor-grpc:${reactorGrpcVersion}"
        }
    }

    generateProtoTasks {
        ofSourceSet("main")*.plugins {
            grpc {}
            reactor {}
        }
    }
}

tasks.every { task ->
    task.dependsOn ':extractProto'
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }

    options.addStringOption('Xdoclint:none', '-quiet')
}

gradlePlugin {
    website = 'https://github.com/boozilla/houston-gradle'
    vcsUrl = 'https://github.com/boozilla/houston-gradle.git'

    plugins {
        houston {
            group = 'io.github.boozilla.houston'
            id = group
            displayName = 'houston'
            description = 'This is a plugin that helps work with the houston server'
            implementationClass = 'boozilla.houston.gradle.HoustonPlugin'

            tags.set(['houston', 'asset', 'sync'])
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform(libs.junit.bom)
    testImplementation libs.junit.jupiter

    implementation project(':api')

    implementation libs.reactor.core
    implementation libs.reactor.tools

    runtimeOnly libs.grpc.netty.shaded
    implementation libs.grpc.api
    implementation libs.grpc.stub
    implementation libs.grpc.protobuf
    implementation libs.protobuf.java
    implementation libs.reactor.grpc.stub

    implementation libs.spring.core

    implementation libs.gradle.api

}

test {
    useJUnitPlatform()
}
