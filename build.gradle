plugins {
    id 'java-library'
    id 'com.google.protobuf'
    id 'com.gradle.plugin-publish' version '1.2.1'
}

group = rootProject.group
version = rootProject.version

sourceSets {
    main {
        proto {
            srcDir rootProject.sourceSets.main.proto.srcDirs
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
        reactor {
            artifact = "com.salesforce.servicelibs:reactor-grpc:${reactorGrpcVersion}"
        }
    }

    generateProtoTasks {
        ofSourceSet("main")*.plugins {
            grpc {}
            reactor {}
        }
    }
}

tasks.every { task ->
    task.dependsOn ':extractProto'
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }

    options.addStringOption('Xdoclint:none', '-quiet')
}

gradlePlugin {
    website = 'https://github.com/boozilla/houston'
    vcsUrl = 'https://github.com/boozilla/houston.git'

    plugins {
        houston {
            group = 'io.github.boozilla'
            id = "${group}.houston"
            displayName = 'houston'
            description = 'This is a plugin that helps work with the houston server'
            implementationClass = 'boozilla.houston.gradle.HoustonPlugin'

            tags.set(['houston', 'asset', 'sync'])
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation project(':api')

    implementation "io.projectreactor:reactor-core:${reactorVersion}"
    implementation "io.projectreactor:reactor-tools:${reactorVersion}"

    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-api:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
    implementation "com.salesforce.servicelibs:reactor-grpc-stub:${reactorGrpcVersion}"

    implementation "org.springframework:spring-core:${springVersion}"

    implementation "dev.gradleplugins:gradle-api:8.4"
}

test {
    useJUnitPlatform()
}